.org 0x3000
SET PC, fizzbuzz

.include "os_jumptable.dasm16"
.include "kern_jumptable.dasm16"

:fizz dat "Fizz", 0
:buzz dat "Buzz", 0
:max dat 20
:numberbuffer dat 0x7100

:fizzbuzz	
	set a, 10 ; length of block we request	
	set b, 0x1337 ; arbitrary memory owner
	jsr [kern_malloc]
	set [numberbuffer], a
	
	set b, 1 ; A B C are clobberable
		
	:fizzbuzz_loop
		set c, b
		mod c, 3 ; check for fizz
		set push, c ; we'll need this for later
		ife c, 0
			jsr do_fizz
		
		set c, b
		mod c, 5
		set push, c
		ife c, 0
			jsr do_buzz
		
		set c, pop
		set a, pop
		ife c, 0
			set pc, do_newline
		ife a, 0
			set pc, do_newline
		
		set push, b
		set a, b
		jsr fizzbuzz_print_number
		set b, pop
		
	:do_newline
		jsr [kern_newline]
			
		add b, 1
		ife [max], b
			set pc, fizzbuzz_end
		
		set pc, fizzbuzz_loop
		
	:fizzbuzz_end
		set a, [numberbuffer]
		jsr [kern_free]
		set pc, [os_enterdone]
		
	:fizzbuzz_set_zero
		set a, [numberbuffer]
		set b, 0
		set c, 10
		jsr [kern_memset] ; make sure memory is set to zero
		set pc, pop
	
	:do_fizz
		set a, fizz
		jsr [kern_print]
		set pc, pop
		
	:do_buzz
		set a, buzz
		jsr [kern_print]
		set pc, pop
		
	:fizzbuzz_print_number
		set push, a
		jsr fizzbuzz_set_zero
		set a, pop
		set b, [numberbuffer]
		set c, 10
		jsr [kern_itoa]
		set a, [numberbuffer]
		add a, 1
		jsr [kern_print]
		set a, [numberbuffer]
		sub a, 1
		set pc, pop
